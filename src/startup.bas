1 REM TEMPORARY LOADER FILE - PRINTS A TEXT FILE TO PREPARE THE SCREEN BEFORE THE GAME STARTS
2 REM WORKS PRETTY MUCH LIKE LOADING A BITMAP IMAGE WITH GAME UI, BUT WITH TEXT DATA INSTEAD
3 REM SUPPORTS UPPERCASE, LOWERCASE, INVERSE AND MOUSE TEXT AND IS USEFUL FOR CREATING TEXT UI
4 REM TRIGGERS: NORMAL [INVERSE] {MOUSETEXT}
5 REM SUBS: "~" IDENT SPACE, ";" TWO VERTICAL LINES

9 I = 1 : B$ = ""
10 D$ = CHR$(4)
12 M$ = CHR$(27) + CHR$(15) : REM TURN SPECIAL CHARACTERS ON (80COL MOUSE TEXT CARD CHARS)
15 N$ = CHR$(24) + CHR$(14) : REM TURN SPECIAL CHARACTERS OFF

20 PRINT D$;"PR#3" : REM TURN 80COL MODE ON

25 ONERR GOTO 100 : REM OPEN AND READ A TEXT FILE, SAVING THE DATA IN AN ARRAY (LINES$)
30 PRINT D$;"OPEN DATA"
35 PRINT D$;"READ DATA"
40 INPUT L : REM GET THE TOTAL LINES OF THE TEXT FILE (FIRST PARAMETER FROM TEXT FILE)
50 INPUT H : HTAB(H) : REM GET LEFT OFFSET WHEN INSERTING THE TEXT (SECOND PARAMETER FROM TEXT FILE)
60 INPUT V : VTAB(V) : REM GET TOP OFFSET WHEN INSERTING THE TEXT (THIRD PARAMETER FROM TEXT FILE)
65 DIM LINES$(L) : REM DEFINE AN ARRAY TO HOLD THE TEXT DATA
70 INPUT A$ : REM GET THE NEXT DATA ROW
80 LINES$(I) = A$ : I = I + 1 : REM SAVE IT
90 GOTO 70 : REM LOOP
100 PRINT D$;"CLOSE"

190 REM PRINTING THE DATA ON SCREEN
200 FOR I = 1 TO L
205   A$ = LINES$(I) : M = LEN(A$) : D = 1
210   VTAB(V + I - 1)
220   FOR X = 1 TO M
230     B$ = MID$(A$, X, 1)
240     C = ASC(B$)
250     IF C = 123 THEN D = D + 1 : HTAB(H + X - D) : ? M$; : GOTO 300 : REM TURN MOUSE TEXT ON WITH TRIGGER: "{"
260     IF C = 125 THEN D = D + 1 : HTAB(H + X - D) : ? N$; : GOTO 300 : REM TURN MOUSE TEXT OFF WITH TRIGGER: "}"
270     IF C = 91 THEN D = D + 1 : HTAB(H + X - D) : ? CHR$(15); : GOTO 300 : REM TURN INVERSE ON WITH TRIGGER: "["
280     IF C = 93 THEN D = D + 1 : HTAB(H + X - D) : ? CHR$(14); : GOTO 300 : REM TURN INVERSE OFF WITH TRIGGER: "]"
285     IF C = 59 THEN HTAB(H + X - D) : ? "\\"; : GOTO 300 : REM TYPE IN BETWEEN VERTICAL SPACE WITH TRIGGER: ";"
290     IF C < 125 THEN HTAB(H + X - D) : ? B$; : REM WILL PRINT THE REQUESTED LETTER, SPACE INTERPRETATION: "~"
300   NEXT X
370 NEXT I

490 REM RUNNING A NEW BASIC FILE SHOULD WIPE ALL THE ABOVE
495 REM USED ONLY AT THE BEGINNING TO DRAW THE TEXT SCREEN UI
500 PRINT D$;"RUN MAIN"
