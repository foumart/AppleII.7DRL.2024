5 REM INITIALIZATION
10 D$ = CHR$(4)
12 MX = 0 : MY = 0 : REM MAP ROOMS COORDINATES
14 RW = 9 : RH = 4 : REM ROOM SIZE
15 X = 23 : Y = 19 : REM INITIAL PLAYER POSITION
20 DIM PLAYER(3) : REM HOLDS X, Y, AND THE MAP CHARACTER THAT'S BEHIND THE UNIT
22 PLAYER(1) = X : PLAYER(2) = Y : PLAYER(3) = 128
24 A = 93 : REM HOLDS THE MAP TILE PLAYER IS CURRENTLY ON READ BY THE READCHAR ROUTINE. (DUNGEON ENTRANCE STAIRS)
26 A$ = CHR$(93) : REM THE CHARACTER CODE READ BY THE ROUTINE BUT AS STRING
30 M$ = CHR$(27) + CHR$(15) : REM TURN SPECIAL CHARACTERS ON (80COL CARD CHARS)
32 N$ = CHR$(24) + CHR$(14) : REM TURN SPECIAL CHARACTERS OFF

40 ? D$;"PR#3"
50 ? D$"BLOAD READCHAR"

60 DIM ROOMS$(4)
70 ROOMS$(1) = "  "
75 ROOMS$(2) = " "+M$+CHR$(93)+N$
80 ROOMS$(3) = M$+CHR$(90)+CHR$(94)+N$
90 ROOMS$(4) = " "+M$+CHR$(91)+N$

99 REM GENERATE MAP CONNECTIONS ARRAY
100 DIM MAP(5, 5)
110 DATA 0,0,3,0,0,0,0,5,0,0,0,2,15,4,0,0,0,5,0,0,0,0,1,0,0
130 FOR I = 0 TO 4
140   FOR J = 0 TO 4
150     READ MAP(I,J)
160   NEXT J
170 NEXT I

199 REM GENERATE MAP CONTENTS ARRAY
200 DIM INSIDES(5, 5)
240 DATA 0,0,2,0,0,0,0,0,0,0,0,0,3,0,0,0,0,0,0,0,0,0,1,0,0
260 FOR I = 0 TO 4
270   FOR J = 0 TO 4
280     READ INSIDES(I,J)
290   NEXT J
300 NEXT I

359 REM DRAW ALL MAP ROOMS
360 FOR ROW = 0 TO 4
370   FOR COL = 0 TO 4
380     REM CALCULATE THE X AND Y COORDINATES OF THE TOP LEFT CORNER OF THE ROOM
390     MX = (COL) * (RW)
400     MY = (ROW) * (RH)
410     REM DRAW A ROOM AT THE CURRENT POSITION
420    GOSUB 1000
430   NEXT COL
440 NEXT ROW

450 REM GOTO 795 : REM DISPLAY DEBUG INFO RIGHT AWAY

499 REM MAIN LOOP
500 REM FOR II=1 TO 500: NEXT II : REM WAIT 49200,3
510 REM IF PLAYER(1) = X AND PLAYER(2) = Y THEN GOTO 550 : REM SKIP IF PLAYER HAS NOT MOVED
530 PLAYER(1) = X : PLAYER(2) = Y : PLAYER(3) = A
540 HTAB(X) : VTAB(Y) : ? N$;"@" : REM DRAW PLAYER
550 IF K > 0 AND PEEK(-16384) > 0 THEN 500
560 K = 0
570 IF PEEK(-16384) = 0 THEN 500
580 K = PEEK(-16384)
590 REM POKE-16336,0 : REM CLEARS KEYBOARD STROBE - ALWAYS AFTER READING KEYBOARD
600 VTAB(22) : ? N$;"keycode: ";K;"  "; : REM DEBUG DISPLAY OF CHAR CODE KEY PRESSED

610 REM DISPLAY THE MAP CHAR AT THE PLAYER'S PREVIOUS POSITION AND PERFORM MOVE
615 HTAB(X) : VTAB(Y) : IF A = 160 THEN ? N$;A$ : GOTO 700 : REM MAKE SURE WE DISPLAY THE PROPER CHARACTER
620 ? M$;A$;N$
700 REM
710 IF K = 149 OR K = 225 OR K = 196 THEN GOTO 750
720 IF K = 136 OR K = 234 OR K = 202 THEN GOTO 760
730 IF K = 139 OR K = 247 OR K = 215 THEN GOTO 770
740 IF K = 138 OR K = 243 OR K = 211 THEN GOTO 780
745 IF K = 160 THEN GOTO 790

748 GOTO 510

750 X = X + 1 : GOSUB 800
755 GOTO 500

760 X = X - 1 : GOSUB 800
765 GOTO 500

770 Y = Y - 1 : GOSUB 800
775 GOTO 500

780 Y = Y + 1 : GOSUB 800
785 GOTO 500

790 GOSUB 950
795 GOTO 500


800 REM PEEK(36) AND POS(0) ARE NOT WORKING IN 80 COL, SO BELOW WE ACCESS DIFFERENT ADDRESSES TO BE SURE
805 POKE 1147,X : POKE 1403,X : POKE 36,X : REM POKE 1531,Y : POKE 37,Y
810 P1 = PEEK (36)
820 IF P1 = 0 THEN P1 = PEEK (1403)
830 IF P1 = 0 THEN P1 = PEEK (1147)
910 POKE 1403,P1-1 : POKE 1147,P1-1 : POKE 36,P1-1 : REM MAKE SURE WE HAVE THE CORRECT HORIZONTAL POSITION
920 VTAB(Y)
930 CALL 32256: REM CALL THE GETCHAR ROUTINE
940 A = PEEK(6) : A$ = CHR$(A)
950 HTAB(2) : VTAB(23) : ? N$;"cursor: ";P1;"x";Y;"     PLAYER symbol: ";A;" (";M$;A$;N$;")   "
980 RETURN

990 ? CHR$(14) : END

1000 REM DRAW A TILE
1005 ? CHR$(15)
1010 HTAB(MX+1) : VTAB(MY+1)

1012 IF MY < 4 THEN GOTO 1016
1014 ? N$;" ";M$;"\\\\\\\\\\\\\\";N$;" ";M$ : REM DRAW IN BETWEEN ROOM VERTICAL SPACE
1015 GOTO 1020
1016 ? N$;" _______ ";M$ : REM DRAW TOP EDGE

1020 M = MAP(ROW, COL)
1025 IF M <> 0 THEN HTAB(MX+1) : VTAB(MY+2) : ? "Z_";N$;"     ";M$;"Z_"
1030 IF M <> 0 THEN HTAB(MX+1) : VTAB(MY+3) : ? "Z_";N$;" ";ROOMS$(INSIDES(ROW, COL)+1);"  ";M$;"Z_"
1040 IF M <> 0 THEN HTAB(MX+1) : VTAB(MY+4) : ? "Z_";N$;"     ";M$;"Z_"

1049 REM GREYED OUT ROOMS
1050 IF M = 0 THEN HTAB(MX+1) : VTAB(MY+2) : ? "ZVWVWVWV_"
1060 IF M = 0 THEN HTAB(MX+1) : VTAB(MY+3) : ? "ZVWVWVWV_"
1070 IF M = 0 THEN HTAB(MX+1) : VTAB(MY+4) : ? "ZVWVWVWV_"

1090 IF M = 1 OR M = 5 OR M = 7 OR M = 8 OR M = 11 OR M > 12 THEN HTAB(MX+4) : VTAB(MY+1) : ? " ^ " : REM DRAW DOOR


1145 IF MY < 15 THEN RETURN
1150 HTAB(MX+1) : VTAB(MY+5) : ? N$;" ";M$;"LLLLLLL";N$;" ";M$
1160 RETURN